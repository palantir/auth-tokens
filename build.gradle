buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.palantir.gradle.externalpublish:gradle-external-publish-plugin:1.2.1'
        classpath 'com.palantir.baseline:gradle-baseline-java:3.81.0'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:1.28.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:0.12.3'
        classpath 'com.palantir.gradle.revapi:gradle-revapi:1.4.4'
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:1.0.1'
        classpath 'gradle.plugin.org.inferred:gradle-processors:3.3.0'
    }
}

apply plugin: 'com.palantir.external-publish'
apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.consistent-versions'
apply plugin: 'com.palantir.git-version'

version gitVersion()

allprojects {
    apply plugin: 'com.palantir.java-format'

    group 'com.palantir.tokens'
    version rootProject.version

    repositories {
        mavenCentral()
        jcenter()
    }

}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.inferred.processors'

    sourceCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.compilerArgs += [
            '-Xlint:deprecation',
            '-Werror'
        ]
    }

    configurations.testCompileClasspath {
        exclude module: 'junit'
    }
}
