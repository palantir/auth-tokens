buildscript {
    repositories {
        gradlePluginPortal()
        maven { url  "http://palantir.bintray.com/releases" }
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'com.netflix.nebula:gradle-dependency-lock-plugin:7.1.2'
        classpath 'com.netflix.nebula:nebula-publishing-plugin:9.1.1'
        classpath 'com.palantir.baseline:gradle-baseline-java:0.46.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:0.11.0'
        classpath 'gradle.plugin.org.inferred:gradle-processors:2.1.0'
    }
}

apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.git-version'

allprojects {
    group 'com.palantir.tokens'

    repositories {
        jcenter()
        maven { url  "http://palantir.bintray.com/releases" }
    }

    version gitVersion()
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.inferred.processors'

    sourceCompatibility = 1.8
    if (gradle.startParameter.taskNames.find {
        it.endsWith('verifyDependencyLocksAreCurrent') || it.endsWith('build')}) {
        gradle.startParameter.setTaskNames(gradle.startParameter.taskNames + ['generateLock'])
    }

    apply plugin: 'nebula.dependency-lock'
    dependencyLock {
        includeTransitives = true
        lockFile = 'versions.lock'
        configurationNames = ['runtime', 'compileClasspath']
    }
    task verifyDependencyLocksAreCurrent {
        doLast {
            def expectedDependencies = tasks.saveLock.getOutputLock()
            def actualDependencies = tasks.saveLock.getGeneratedLock()
            def digester = java.security.MessageDigest.getInstance('SHA')
            logger.info("Verifying integrity of dependency locks: {} vs {}", expectedDependencies, actualDependencies)
            if (digester.digest(expectedDependencies.bytes) != digester.digest(actualDependencies.bytes)) {
                throw new GradleException("The dependencies of project " + project.name + " do not match the expected "
                    + "dependencies recorded in " + expectedDependencies + ". "
                    + "Run `./gradlew generateLock saveLock` and commit the updated version.lock files")
            }
        }
    }

    verifyDependencyLocksAreCurrent.dependsOn(generateLock)
    check.dependsOn(verifyDependencyLocksAreCurrent)
}
